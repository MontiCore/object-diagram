/* (c) https://github.com/MontiCore/monticore */

/* Beta-version: This is intended to become a MontiCore stable grammar. */

package de.monticore.lang;

/**
 * ODBasics defines the core structure of object diagrams.
 * It contains the artifact infrastructure (import, starting keyword,
 * a conforms statement that points to artifacts (class diagrams) that it 
 * conforms to).
 * An object diagrams contains objects and links.
 * Objects have attributes of varios basic or OO types and values. 
 * Values can be arbitrary expressions (or just single constant literals).
 *
 * This grammar is designed for extension, e.g.
 * * expressions
 * * more complex forms of values
 */
grammar ODBasics extends de.monticore.types.MCFullGenericTypes,
                         de.monticore.UMLModifier,
                         de.monticore.expressions.ExpressionsBasis,
                         de.monticore.types.BasicTypeSymbols {
      
    /*========================================================================*/
    /*=========================== Artifact ===================================*/
    /*========================================================================*/
    
    /** ASTODArtifact represents the complete Diagram
      @attribute package The package declaration of this Objectdiagram
      @attribute importStatements List of imported elements
      @attribute oDConformsStatement List of conform statements
      @attribute objectDiagram the ObjectDiagram
    */
    ODArtifact =
      ("package" package:(Name& || ".")+ ";")?
      MCImportStatement*
      ODConformsStatement*
      ObjectDiagram;

    /*========================================================================*/
    /*================== INTERFACES AND EXTERNAL SYMBOLS =====================*/
    /*========================================================================*/

    /**
     * ASTODValue represents the interface for all possible values
     */
    interface ODValue;

    /*========================================================================*/
    /*============================ ENUMERATIONS ==============================*/
    /*========================================================================*/

    /**
    *enum AssocDirection =
    *  LEFTTORIGHT:"->" | RIGHTTOLEFT:"<-"| BIDIRECTIONAL:"<->" | UNSPECIFIED:"--";
    */

    /*========================================================================*/
    /*============================== Grammar =================================*/
    /*========================================================================*/

    /** ASTODConformsStatement defins, to which artifact (normally a class diagrams)
      the object diagram shall conform (this may act as a syntax check)
      @attribute qualifiedName Qualified name of the artifact the OD is conform to
    */
    ODConformsStatement =
      "conformsTo" MCQualifiedName ";";

    /** ASTObjectDiagram represents a UML Objectdiagram
      consisting of object and link definitions.
      @attribute stereotype  Optional Stereotype
      @attribute name        Name of this Objectdiagram
      @attribute oDObject    List of Objects of this Objectdiagram
      @attribute oDLink      List of Links of this Objectdiagram
    */
    symbol scope ObjectDiagram =
      Stereotype?
      "objectdiagram" Name
      "{"
        ( ( ODObject | ODLink ) ";" )*
      "}";

    /** AST-ODObject represents an Object in a UML Objectdiagram.
      The object contains attributes and may be hierarchically decomposed.
      @attribute modifier Optional Modifier of this object
      @attribute oDName Optional extended name of this object
      @attribute referenceType Required type of this object
      @attribute oDAttributes List of attributes of this object
    */
    symbol ODObject implements ODValue <20> =
      Modifier?
      ODName? ":" MCType
        "{" ODAttribute* "}";

    // ODObject does not have a Name nonterminal.
    // This method acts as surrogate (which is accepted as well):
    astrule ODObject =
      method public String getName() {
        if (isPresentODName()) {
            return getODName().getName();
         }
    // TODO SH: Folgendes ist absolut nicht statthaft: Da muss mindestens eine Exception geworfen werden!
    // TODO SH: (Könnte man getName auch Optional liefern lassen?
        return "";
    };

    /** ASTODAttribute represents an Attribute of an Object.
      The attribute may be List, Set or Map valued.
      Furthermore, it is possible to specify the collection as incomplete
      using "->" or as complete with an equality "=".
      @attribute stereotype Optional stereotype of this attribute
      @attribute modifier Optional modifier of this attribute
      @attribute type Optional type of this attribute
      @attribute name Name of this Attribute
      @attribute complete True if attribute has a "complete" assignment
      @attribute oDValue Attribute value in form of an ODValue
      @attribute oDList Attribute value in form of an ODList
      @attribute oDMap Attribute value in form of an ODMap
    */
    ODAttribute =
      Stereotype?
      Modifier?
      MCType?
      Name
      ( (complete:"="|{noSpace(2)}? "-" ">") ( ODValue | ODList | ODMap | ODExpression ) )? ";";

    /** ASTODList represents a List of ODValues.
        (Whether the order is relevant is not decided here.
        Therefore, it is also possible to store sets this way.)
      @attribute oDValue List of ODValue
    */
    ODList =
      "[" (ODValue || ",")* "]";

    /** ASTODMap represents a Map of values
      @attribute oDMapElement List of ODMapElements
    */
    ODMap =
      "[" (ODMapElement || ",")* "]";

    /** ASTODMapElement represents a Map element of ODMap.
        It can be either a concrete ODObject or a concrete ODValue.
      @attribute key Key element of the key value pair
      @attribute val Value element of the key value pair
    */
    ODMapElement =
      key:ODValue {noSpace(2)}? "-" ">" val:ODValue;

    /** ASTODLinkQualifier represents Qualifier for Links between Objects.
        It can be either a concrete attribute name or a concrete value.
      @attribute name Name of a referenced attribute (if set)
      @attribute oDValue Concrete value of this qualifier (if set)
    */
    ODLinkQualifier = "[[" Name "]]" | "[" ODValue "]";

    /** ASTODLink represents a Link between Objects
 TODO: Attributeangaben hier nochmal prüfen (und an anderen Stellen ggf. auch)
      @attribute stereotype Optional Stereotype
      @attribute Link True if Link is of type "link"
      @attribute Aggregation True if Link is of type "aggregation"
      @attribute Composition True if Link is of type "composition"
      @attribute derived True if this is a derived Link
      @attribute Name Name of the Association of this Link
      @attribute leftModifier Optional left side Modifier
      @attribute leftReferenceNames List of References of the Objects on the left side of this Link
      @attribute leftQualifier Qualifier of the left side of this Link
      @attribute leftRole Role of Objects on the Links left side
      @attribute leftToRight True if Link is navigable from left to right ("->")
      @attribute rightToLeft True if Link is navigable from right to left ("<-")
      @attribute bidirectional True if Link is navigable in both directions ("<->")
      @attribute unspecified True if navigation of Link is not specified ("--")
      @attribute rightRole Role of Objects on the Links right side
      @attribute rightQualifier Qualifier of the right side of this Link
      @attribute rightReferenceNames List of References of the Objects on the right side of this Link
      @attribute rightModifier Optional right side Modifier
    */
    ODLink =
      Stereotype?
      (["link"] | ["aggregation"] | ["composition"])
      ([derived:"/"])?
      Name?
      ODLinkLeftSide
      ODLinkDirection
      ODLinkRightSide;

    interface ODLinkDirection;

    ODLeftToRightDir implements ODLinkDirection = {noSpace(2)}? "-" ">";
    ODRightToLeftDir implements ODLinkDirection = {noSpace(2)}? "<" "-";
    ODBiDir          implements ODLinkDirection = {noSpace(2, 3)}? "<" "-" ">";
    ODUnspecifiedDir implements ODLinkDirection = {noSpace(2)}? "-" "-";

    interface ODLinkSide =
      Modifier?
      referenceName:(ODName || ",")+
      ODLinkQualifier?
      ("(" role:Name ")")?;

    ODLinkLeftSide implements ODLinkSide =
      Modifier?
      referenceName:(ODName || ",")+
      ODLinkQualifier?
      ("(" role:Name ")")?;

    ODLinkRightSide implements ODLinkSide =
       ("(" role:Name ")")?
       ODLinkQualifier?
       referenceName:(ODName || ",")+
       Modifier?;

    /** ASTODAbsent represents implementation of the ODValue interface
        that represents an absent optional value.
      @attribute absent True if attribute is optional and has an empty value
    */
// TODO: muss der "_" sein? Muss der Name überhaupt sein? 
// Könnten wir auch "..." hier einsetzen? (machen wir im SD ähnlich)
    ODAbsent implements ODValue <10> =
      _absent:["absent"];

    ODLiteral implements ODValue <40> =
      Literal;

    ODExpression implements ODValue <30> =
      Expression;

    ODName implements ODValue <50> = Name;

}
